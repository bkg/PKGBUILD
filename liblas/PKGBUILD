# Maintainer: Patryk Kowalczyk <patryk AT kowalczyk DOT WS>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>

pkgname=liblas
pkgver=1.8.0
pkgrel=1
pkgdesc='Library for embedding read and write support of the ASPRS LAS format into your own C/C++ applications, and a suite of command-line utilities based on LASTools for inspecting, manipulating, transforming, and processing LAS LiDAR data'
arch=(i686 x86_64)
url='http://www.liblas.org/'
license=('BSD')
depends=('boost-libs' 'gdal' 'laszip>=2.0.1')
makedepends=('boost' 'cmake')
optdepends=('oci: Oracle Instant Client, see http://www.liblas.org/compilation.html (must recompile)')
provides=(liblas)
conflicts=(liblas-hg)
source=("https://github.com/libLAS/libLAS/archive/${pkgver}.tar.gz")
md5sums=('e7f86bf90166a3ee3de2bc57bb809972')

build() {
  cd "$srcdir/libLAS-$pkgver"

  msg 'Configuring build...'
  cmake -DWITH_GEOTIFF=ON \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DWITH_GEOTIFF=ON \
    -DWITH_GDAL=ON \
    -DWITH_LASZIP=ON \
    -G 'Unix Makefiles' .

  msg 'Building...'
  make
}

package() {
  cd "$srcdir/libLAS-$pkgver"
  make DESTDIR="$pkgdir/" install
}
