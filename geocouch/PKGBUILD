# Maintainer: Leif Warner <abimelech@gmail.com>
pkgname=geocouch
pkgver=1.2.1
pkgrel=1
pkgdesc="A CouchDB add-on to support geospatial indexing and queries."
arch=('i686' 'x86_64')
url="http://github.com/couchbase/geocouch"
license=('APACHE')
depends=('couchdb')
source=('https://github.com/couchbase/geocouch/tarball/couchdb1.2.x')
install=geocouch.install
md5sums=('9e7b9436b61a2861f736d40e232d2e98')
_gitrev=8c01f93

build() {
    cd "$srcdir/couchbase-geocouch-$_gitrev"
    export COUCH_SRC=/usr/lib/couchdb/erlang/lib/couch-${pkgver}/include
    sed -i 2d Makefile  # We're not in a git repo.
    make buildandtest
}

package(){
    cd "$srcdir/couchbase-geocouch-$_gitrev"
    install -d -m755 -o couchdb -g daemon $pkgdir/etc/couchdb/default.d
    install -m644 -o couchdb -g daemon etc/couchdb/default.d/geocouch.ini $pkgdir/etc/couchdb/default.d/geocouch.ini
    install -d $pkgdir/usr/share/couchdb/www/script/test
    install -t $pkgdir/usr/share/couchdb/www/script/test share/www/script/test/* 
    install -d $pkgdir/usr/lib/couchdb/erlang/lib/geocouch/ebin
    install -t $pkgdir/usr/lib/couchdb/erlang/lib/geocouch/ebin ebin/* 
}
